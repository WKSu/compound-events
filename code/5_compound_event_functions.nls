to compound_event_impact
  ; create the compound event around the location of the volcano - use it as epicenter
  ; patch 186 94 is the patch location found for the volcano in setup-volcano

  ; the Laacher See Eruption most-likely started in late spring/early summer (H.-J. Schweitzer Entstehung und Flora des Trasses im nördlichen Laacher See-Gebiet Eiszeitalter und Gegenwart, 9 (1958), pp. 28-48)
  ; the bulk of the magma volume was erupted in 10 hours, the ash fall lasted a few weeks but not more than a few months Schmincke, H. U., Park, C., & Harms, E. (1999). Evolution and environmental impacts of the eruption of Laacher See Volcano (Germany) 12,900 a BP. Quaternary International, 61(1), 61-72.
  ; this is why the compound event is modelled into one tick
  if ticks = start_event [
    ; create the volcano impact
    ask volcanoes [
      ; in-radius asks all patches that is inside this distance
      ask patches in-radius volcano_eruption_distance  [
        set volcano_impact? true
      ]
    ]

    ask volcanoes [
      ; in cone asks for a distance and the angle
      ; because ash falls based on the wind this can be used
      ; take into account NE > S > SW from The eruptive centre of the late Quaternary Laacher See tephra Geologische Rundschau, 73 (3) (1984), pp. 933-980
      ; initial parameters are set so ash fall is similar to figure 1 in Reinig, F., Cherubini, P., Engels, S., Esper, J., Guidobaldi, G., Jöris, O., ... & Pfanz, H. (2020). Towards a dendrochronologically refined date of the Laacher See eruption around 13,000 years ago. Quaternary Science Reviews, 229, 106128.

      if ash_fallout = "in-radius" [
        ask patches in-radius ash_eruption_radius [
          set ash_impact? true
        ]
      ]

      if ash_fallout = "wind-cones" [
        ; set the ash to a specific wind direction, eruption distance, and spread of this cone
        set heading ash_wind_direction_1
        ask patches in-cone ash_eruption_distance_1 ash_eruption_angle_1 [
          set ash_impact? true
        ]

        set heading ash_wind_direction_2
        ask patches in-cone ash_eruption_distance_2 ash_eruption_angle_2 [
          set ash_impact? true
        ]

        ; take the average of the distances for 'random' ash fall
        let average_distance (ash_eruption_distance_1 + ash_eruption_distance_2) / 2
        let available_patches average_distance ^ 2 * 3.14 ; calculate surface area of possible patches with patches
        let random_patches round (available_patches * ( random_ash_fall / 100 ))

        ask n-of random_patches patches in-radius average_distance [
          ; random impact of ash fall on the world to create ash outside the cones
          set ash_impact? true
          ask one-of neighbors [ set ash_impact? true ]
        ]

        set heading 0 ; set the volcano back to its original location
      ]

    ]

    ; create either a normal or skewed distribution of the ash eruption
    ; normalize ash impact between 0 and 100
    if ash_eruption_distribution = "normal" [
      ask patches with [ ash_impact? = true ] [
        set ash_fall min list 100 random-normal mean_ash_intensity stdv_ash_intensity ]
    ]

    if ash_eruption_distribution = "skewed far" [
      let max_distance 0 
      if ash_fallout = "wind-cones" [ 
        set max_distance max list ash_eruption_distance_1 ash_eruption_distance_2 ]
      if ash_fallout = "in-radius" [ 
        set max_distance ash_eruption_radius ] 

      ask patches with [ ash_impact? = true ] [
        set ash_fall min list 100 random-poisson ((distance patch 186 95) / (max_distance) * 100) 
      ]
    ]

    if ash_eruption_distribution = "skewed near" [
      let max_distance 0 
      if ash_fallout = "wind-cones" [ 
        set max_distance max list ash_eruption_distance_1 ash_eruption_distance_2 ]
      if ash_fallout = "in-radius" [ 
        set max_distance ash_eruption_radius ] 

      ask patches with [ ash_impact? = true ] [
        set ash_fall min list 100 random-poisson ((1 - (distance patch 186 95 / max_distance)) * 100)
      ]
    ]
  ]
  ; impact of the LSE could have lasted for 6 years     Kaiser, K.F., 1993. Klimageschichte vom späten Hochglazial bis ins frühe Holozän, rekonstruiert mit Jahrringen und Molluskenschalen aus verschiedenen Vereisungsgebieten. Eidgenössische Forschungsanstalt für Wald, Schnee und Landschaft, Zürich, pp. 1–203.
  interaction_volcano
  
  visualize-impact-volcano
end

to visualize-impact-volcano

  
  if ticks = 0 or ticks = start_event + volcano_duration_effect [
    ; start: coloring patches to represent european landmass 13900 - 12700BP
    let min-landmass gis:minimum-of europe-landmass
    let max-landmass gis:maximum-of europe-landmass

    ask patches [
      if (landmass <= 0) or (landmass >= 0) ; note the use of the "<= 0 or >= 0" technique to filter out "not a number" values
      [ set pcolor scale-color black landmass min-landmass max-landmass ]

      if (landmass = 781.4310302734375) or (landmass = 1133.7154541015625) or (landmass = 0) ;; easy way to idenfity water bodies
      [ set pcolor blue ]
    ]
  ]
  
  if show_impact = true [
    if ticks = start_event [ 
      ask patches with [ ash_impact? = true ] [
        set pcolor scale-color orange ash_fall 0 100
      ]
      ask patches with [ volcano_impact? = true ] [ 
        set pcolor red 
      ]
      
      ask bands with [ event_impact? = true ] [ 
        set color red 
      ]
    ]
    
    ask patches with [ ash_impact? = true ] [
        set pcolor scale-color orange ash_fall 0 100
    ]
  ]
end

to interaction_volcano
  ; kill all hunter-gatherer bands from the eruption of the volcano
  ;
  if ticks = start_event [
    ask patches with [ volcano_impact? = true ] [
      ask bands-here [ 
        set death_by_volcano death_by_volcano + 1
        set event_cultural_capital_loss event_cultural_capital_loss + cultural_capital
        die 
      ]
      set food_available 0
      set resources_available 0
    ]
    
    ask patches with [ ash_impact? = true ] [
      ; lower the max food and resources of a patch based on the ash fall
      set max_food min list 0 max_food_patch * ( 1 - ash_fall / 100)
      set max_resources min list 0 max_resource_patch * (1 - ash_fall / 100)
      
      ask bands-here [ 
        set event_impact? true
        set lost_resources lost_resources + resources_owned
        set resources_owned 0 ] ; assumption that the bands are not able to move any extra resources during a catastrophe
    ]
  ]
  
  ; decrease ash fall gradually based on the volcano duration effect
  ask patches with [ ash_impact? = true ] [
    let ash_fall_decrease ash_fall / volcano_duration_effect
    set ash_fall ash_fall - ash_fall_decrease
  ]

  if ticks = start_event + volcano_duration_effect [
    ask patches with [ volcano_impact? = true or ash_impact? = true ] [
      set volcano_impact? false
      set ash_impact? false
      set ash_fall 0
    ]
  ]
end
